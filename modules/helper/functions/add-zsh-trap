# ---------------------------------------------------------
# vim: ts=4 ts=2 ft=zsh
#
# File: ./helper/functions/add-zsh-trap.sh
#
# Provides for trapping UNIX signals and calling callback functions
# when a trap is triggered.
#
# Author: Ernie Lin
# Update: 2025/03/26 with help of ChatGPT
# ---------------------------------------------------------

# Adds a function name to a list to be called when a trap is triggered.
add-zsh-trap() {
	# Validate inputs.
	(( $# < 2 )) && print "usage: $0 type function" >&2 && return 1
	[[ -z "$signals[(r)$1]" ]] && print "$0: unknown signal: $1" >&2 && return 1

	local trap_functions="TRAP${1}_FUNCTIONS"

	# Initialize the array if not already defined.
	(( ! ${(P)+trap_functions} )) && typeset -gaU "$trap_functions"

  # Add the function name to the trap function list.
	eval "$trap_functions+=$2"

	# Define the trap function if not already defined
	(( ! $+functions[TRAP${1}] )) && eval "
		function TRAP${1} {
			for trap_function in \"\$TRAP${1}_FUNCTIONS[@]\"; do
				\"\$trap_function\" \"\$1\"
			done
			return \$(( 128 + \$1 ))
		}
	"
}

add-zsh-trap "$@"